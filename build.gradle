plugins {  
  id 'com.gradleup.shadow' version '8.3.9'
  id 'java'
  id 'base'
  id 'com.diffplug.spotless' version '6.25.0'
  id 'checkstyle'
}

group = 'de.stamme'
version = '0.6.3'

base {
  archivesName = "BasicQuests"
}

java {  
  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21
}

spotless {
  java {
    target 'src/**/*.java'
    eclipse().configFile('config/eclipse-formatter.xml')
    removeUnusedImports()
    trimTrailingWhitespace()
    endWithNewline()
  }
}

checkstyle {
  toolVersion = '10.12.4'
  configFile = file("$rootDir/config/checkstyle/checkstyle.xml")
  showViolations = true
}

repositories {
	mavenCentral()
	maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
  maven { url = 'https://oss.sonatype.org/content/groups/public/' }
	maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
  maven { url = 'https://repo.minebench.de/' }
  maven { url = 'https://repo.essentialsx.net/releases' }
}

dependencies {
	compileOnly 'org.spigotmc:spigot-api:1.21.11-R0.1-SNAPSHOT'
  compileOnly 'me.clip:placeholderapi:2.11.7'
  compileOnly 'org.jetbrains:annotations:26.0.2-1'
  compileOnly 'net.essentialsx:EssentialsXDiscord:2.20.1'

  compileOnly('com.github.MilkBowl:VaultAPI:1.7') {
    exclude group: 'org.bukkit', module: 'bukkit'
  }

  implementation 'de.themoep:minedown:1.7.0-SNAPSHOT'
  implementation 'org.bstats:bstats-bukkit:3.1.0'
  implementation 'fr.mrmicky:fastboard:2.1.5'

  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
	testImplementation 'org.mockito:mockito-core:4.6.1'
	testImplementation 'org.powermock:powermock-mockito-release-full:1.6.4'
}

shadowJar {
  archiveClassifier.set(null)
	destinationDirectory = file(devServerPathPaper21_11)

	relocate 'org.bstats', 'de.stamme.basicquests.lib.bstats'
	relocate 'fr.mrmicky.fastboard', 'de.stamme.basicquests.lib.fastboard'
  relocate 'de.themoep.minedown', 'de.stamme.basicquests.lib.minedown'
}

configurations {
  testImplementation.extendsFrom compileOnly
}

processResources {
  def props = [version: version]
  inputs.properties props
  filteringCharset 'UTF-8'
  filesMatching('plugin.yml') {
    expand props
  }
}

check.dependsOn spotlessApply, checkstyleMain, checkstyleTest

shadowJar.dependsOn check
