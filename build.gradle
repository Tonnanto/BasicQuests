plugins {
  id 'com.github.johnrengelman.shadow' version '8.1.1'
  id 'java'
  id 'base'
}

group = 'de.stamme'
version = '0.5.1'

base {
  archivesName = "BasicQuests"
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
	mavenCentral()
	maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
  maven { url = 'https://oss.sonatype.org/content/groups/public/' }
	maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
  maven { url = 'https://repo.minebench.de/' }
  maven { url = 'https://repo.essentialsx.net/releases' }
}

dependencies {
	compileOnly 'org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT'
	compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
	compileOnly 'me.clip:placeholderapi:2.11.5'
  compileOnly 'org.jetbrains:annotations:24.0.1'
  compileOnly 'net.essentialsx:EssentialsXDiscord:2.20.1'

  implementation 'de.themoep:minedown:1.7.0-SNAPSHOT'
	implementation 'org.bstats:bstats-bukkit:3.0.0'
	implementation 'fr.mrmicky:fastboard:2.0.2'

  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
	testImplementation 'org.mockito:mockito-core:4.6.1'
	testImplementation 'org.powermock:powermock-mockito-release-full:1.6.4'
}

shadowJar {
  archiveClassifier.set(null)
	destinationDirectory = file(devServerPathPaper20_4)

	relocate 'org.bstats', 'de.stamme.basicquests.lib.bstats'
	relocate 'fr.mrmicky.fastboard', 'de.stamme.basicquests.lib.fastboard'
  relocate 'de.themoep.minedown', 'de.stamme.basicquests.lib.minedown'
}

configurations {
  testImplementation.extendsFrom compileOnly
}

test {
	// useJUnitPlatform()
}

processResources {
  def props = [version: version]
  inputs.properties props
  filteringCharset 'UTF-8'
  filesMatching('plugin.yml') {
    expand props
  }
}

build.dependsOn shadowJar

